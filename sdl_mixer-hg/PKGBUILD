# Maintainer: Zachary Massia <zmassia@gmail.com>

pkgname=sdl_mixer-hg
_hgname=SDL_mixer
pkgver=642.dae7bb0a66b1
pkgrel=1
pkgdesc="Audio mixer add-on for SDL 2.0"
arch=('i686' 'x86_64')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('ZLIB')
depends=('sdl-hg' 'glibc')
makedepends=('mercurial')
options=(!libtool)
source=("hg+http://hg.libsdl.org/$_hgname")
md5sums=("SKIP")

pkgver() {
  cd $_hgname
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd $_hgname
  sh autogen.sh
  mkdir build && cd build
  ../configure --prefix=/usr
  make
}

package() {
  cd $_hgname/build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$_hgname/COPYING.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
